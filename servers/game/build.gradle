import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.20'
    id 'java'
    id 'application'
    id 'jacoco'
}

dependencies {
    compile project(':core')
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.8.0'
    compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.14.0'
    compile group: 'io.netty', name: 'netty-all', version: '4.1.50.Final'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.20"
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
    testImplementation("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.1")
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                'fr/rob/entities/**',
                'fr/rob/game/infrastructure/**',
                'fr/rob/game/domain/network/**',
                'fr/rob/game/domain/setup/tasks/TaskAuthCollectJWTPublicKey.class',
                'fr/rob/game/domain/setup/tasks/CertsResponse.class',
                'fr/rob/game/Main**'
            ])
        }))
    }

    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }

}

jacoco {
    toolVersion = "0.8.6"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'fr.rob.game.Main',
                'Class-Path': configurations.compile.collect { it.getAbsolutePath() }.join(' ')
        )
    }
}

// https://stackoverflow.com/a/36130467
tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

mainClassName = 'fr.rob.game.Main'
